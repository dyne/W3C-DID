rule input encoding base58
rule output encoding base58

Scenario eddsa

# load the spec admin keyring to sign the request
Given my name is in a 'string' named 'controller'
and I have my 'keyring'
and I have a 'string' named 'signer_did_spec'

# load eddsa pk for id
Given I have a 'eddsa_public_key'
and I rename 'eddsa_public_key' to 'identity pk'
and I have a 'string' named 'did_spec'

# timestamp
Given I have a 'integer' named 'timestamp'

# broadcast_id
When I set 'broadcast_id' to 'did:dyne:' as 'string'
and I append 'did_spec' to 'broadcast_id'
and I append the string ':' to 'broadcast_id'
and I append the 'base58' of 'identity pk' to 'broadcast_id'

# id
When I create the eddsa public key
and I set 'id' to 'did:dyne:' as 'string'
and I append 'signer_did_spec' to 'id'
and I append the string ':' to 'id'
and I append the 'base58' of 'eddsa public key' to 'id'

# eddsa singature of deactivate_id
When I create the 'string dictionary' named 'signed_by_eddsa'
and I copy 'broadcast_id' in 'signed_by_eddsa'
and I copy 'timestamp' in 'signed_by_eddsa'
and I create the json of 'signed_by_eddsa'
and I create the eddsa signature of 'json'

Then print the 'broadcast_id'
Then print the 'id'
Then print the 'timestamp'
Then print the 'eddsa signature'